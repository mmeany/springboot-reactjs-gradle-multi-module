plugins {
    id 'java'
    alias(libs.plugins.springDependencyManagementPlugin) apply(true)
    alias(libs.plugins.springBootPlugin) apply(true)
}

dependencies {
    api project(':greetingLibrary')
    api libs.bundles.springBootWeb

    annotationProcessor libs.springBootAnnotationProcessor

    testImplementation libs.springBootStarterTest
}

tasks.register('cleanStatic', Delete) {
    delete "${projectDir}/src/main/resources/static"
}

clean.dependsOn cleanStatic

tasks.register('copyFrontendArtifacts', Copy) {
    dependsOn ':helloUI:clean', ':helloUI:npmBuild'
    from "${project(':helloUI').projectDir}/dist"
    into "${projectDir}/src/main/resources/static"
    onlyIf {
        file("${project(':helloUI').projectDir}/dist").exists()
    }
}

processResources.dependsOn copyFrontendArtifacts

bootJar.dependsOn processResources

springBoot {
    buildInfo()
}

bootJar {
    manifest {
        attributes("Implementation-Version": project.version)
    }
}

tasks.named('sourcesJar') {
    dependsOn copyFrontendArtifacts
}
