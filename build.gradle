/*
    This build has been tailored to work with GitHub Actions. It can be provided with the following properties:
    -PprojVersion=x.y.z
    -PecrRepo=NNNNNNNNN.dkr.ecr.eu-west-1.amazonaws.com/sbgmm

    The build will default to 0.0.0-SNAPSHOT and docker.io/library if these properties are not provided.
 */

plugins {
    alias(libs.plugins.springDependencyManagementPlugin) apply(true)
    alias(libs.plugins.springBootPlugin) apply(false)
    id 'base'
    id 'com.mgd.core.gradle.s3' version '2.1.1'
}

allprojects {
    apply plugin: 'java-library'
    //apply plugin: 'java-test-fixtures'
    apply plugin: 'com.mgd.core.gradle.s3'

    group = 'net.mmeany.play'

    // projVersion can be set using -PprojVersion=x.y.z on the command line or GHA
    if (project.hasProperty('projVersion')) {
        project.version = project.projVersion
    } else {
        project.version = '0.0.0-SNAPSHOT'
    }

    if (project.hasProperty('ecrRepo')) {
        // ecrRepo can be set using -PecrRepo=NNNNNNNNN.dkr.ecr.eu-west-1.amazonaws.com/sbgmm on the command line or GHA
        project.ext.set('ecrRepo', project.ecrRepo)
    } else {
        project.ext.set('ecrRepo', 'docker.io/library')
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    s3 {
        bucket = System.getProperty("aws.bucketName", "mvm-releases")
        region = System.getProperty("aws.region", "eu-west-1")
    }

    dependencies {
        implementation(platform(libs.springBootBom))
        implementation libs.bundles.jackon

        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        testImplementation libs.bundles.junitTest
        testRuntimeOnly libs.junitJupiterPlatform

//        testFixturesCompileOnly "org.projectlombok:lombok"
//        testFixturesAnnotationProcessor "org.projectlombok:lombok"
//        testFixturesImplementation "com.fasterxml.jackson.core:jackson-databind"
//        testFixturesImplementation "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
    }
}

static def camelToKebab(String camel) {
    return camel.replaceAll(/([a-z])([A-Z]+)/, '$1-$2').toLowerCase()
}
